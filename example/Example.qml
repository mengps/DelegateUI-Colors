import QtQuick
import QtQuick.Layouts
import QtQuick.Controls.Basic
import DelegateUI.Utils

Window {
    id: window
    width: 1600
    height: 850
    visible: true
    title: qsTr("DelegateUI-Colors Example")

    property bool light: !themeSwitch.checked

    Component.onCompleted: lightChanged();

    Rectangle {
        id: background
        anchors.fill: parent
        color: "#f0f0f0"

        Rectangle {
            id: circle
            x: r - width
            y: -height * 0.5
            width: 0
            height: 0
            radius: width * 0.5
            color: "#141414"
            property real r: Math.sqrt(parent.width * parent.width + parent.height * parent.height)

            NumberAnimation {
                running: !window.light
                properties: "width,height"
                target: circle
                from: 0
                to: circle.r * 2
                duration: 800
                easing.type: Easing.InOutCubic
            }

            NumberAnimation on width {
                running: window.light
                properties: "width,height"
                target: circle
                from: circle.r * 2
                to: 0
                duration: 800
                easing.type: Easing.InOutCubic
            }
        }
    }

    DelColorGenerator {
        id: colorGenerator
    }

    component ColorView: Column {
        id: __column
        spacing: 5
        property alias title: __title.text
        property alias desc: __desc.text
        property alias model: __listView.model
        property string colorName: ""
        property real lineWidth: 180
        property real lineHeight: 30
        property real stretch: 15

        Text {
            id: __title
            anchors.horizontalCenter: parent.horizontalCenter
            color: window.light ? "#5c6b77" : "white"
            font.pointSize: 14

            Behavior on color { ColorAnimation { duration: 800 } }
        }

        Text {
            id: __desc
            anchors.horizontalCenter: parent.horizontalCenter
            color: window.light ? "#5c6b77" : "white"

            Behavior on color { ColorAnimation { duration: 800 } }
        }

        ListView {
            id: __listView
            width: __column.lineWidth
            height: count * __column.lineHeight
            anchors.horizontalCenter: parent.horizontalCenter
            interactive: false
            delegate: Rectangle {
                id: __rootItem
                width: hovered ? __column.lineWidth + __column.stretch : __column.lineWidth
                height: __column.lineHeight
                topRightRadius: hovered ? 4 : 0
                bottomRightRadius: hovered ? 4 : 0
                color: modelData

                property bool hovered: false

                Behavior on width { NumberAnimation { } }
                Behavior on topRightRadius { NumberAnimation { } }
                Behavior on bottomRightRadius { NumberAnimation { } }

                Text {
                    anchors.verticalCenter: parent.verticalCenter
                    anchors.left: parent.left
                    anchors.leftMargin: 10
                    color: (window.light ? (index < 5) : (index >= 5)) ? "#444" : "#f0f0f0"
                    text: __column.colorName + "-" + (index + 1)
                    font.bold: true
                }

                Text {
                    id: __text
                    anchors.verticalCenter: parent.verticalCenter
                    anchors.right: parent.right
                    anchors.rightMargin: 5
                    color: (window.light ? (index < 5) : (index >= 5)) ? "#444" : "#f0f0f0"
                    text: String(modelData).toUpperCase()
                    font.bold: true
                    opacity: __rootItem.hovered ? 1 : 0

                    Behavior on opacity { NumberAnimation { } }
                }

                ToolTip {
                    id: __tooltip
                    enter: Transition {
                        NumberAnimation { property: "opacity"; from: 0.0; to: 1.0; duration: 300 }
                    }
                    exit: Transition {
                        NumberAnimation { property: "opacity"; from: 1.0; to: 0.0; duration: 300  }
                    }
                }

                MouseArea {
                    anchors.fill: parent
                    hoverEnabled: true
                    onEntered: __rootItem.hovered = true;
                    onExited: __rootItem.hovered = false;
                    onClicked: {
                        qmlApi.setClipboardText(__text.text);
                        __tooltip.show(qsTr("å·²å¤åˆ¶åˆ°å‰ªåˆ‡æ¿: ") + __text.text, 300);
                    }
                }
            }
            populate: Transition {
                NumberAnimation { property: "opacity"; from: 0; to: 1; duration: 800 }
            }
        }
    }

    Switch {
        id: themeSwitch
        anchors.right: parent.right
        height: 40
        text: checked ? "ğŸŒ™" : "ğŸ”†"
        font.pointSize: 18
    }

    TabBar {
        id: bar
        width: parent.width
        anchors.bottom: parent.bottom
        currentIndex: swipeView.currentIndex

        TabButton {
            text: qsTr("è°ƒè‰²æ¿æ ·ä¾‹")
        }

        TabButton {
            text: qsTr("è‡ªå®šä¹‰è‰²æ¿ç”Ÿæˆ")
        }
    }

    SwipeView {
        id: swipeView
        width: parent.width
        anchors.top: themeSwitch.bottom
        anchors.bottom: bar.top
        currentIndex: bar.currentIndex
        interactive: false

        Item {
            GridLayout {
                anchors.fill: parent
                columns: 6
                rows: 2
                uniformCellWidths: true
                uniformCellHeights: true

                Repeater {
                    model: [
                        { title: qsTr("Dust Red / è–„æš®"), desc: qsTr("æ–—å¿—ã€å¥”æ”¾"), color: DelColorGenerator.Preset_Red, colorName: "red" },
                        { title: qsTr("Volcano / ç«å±±"), desc: qsTr("é†’ç›®ã€æ¾æ¹ƒ"), color: DelColorGenerator.Preset_Volcano, colorName: "volcano" },
                        { title: qsTr("Sunset Orange / æ—¥æš®"), desc: qsTr("æ¸©æš–ã€æ¬¢å¿«"), color: DelColorGenerator.Preset_Orange, colorName: "orange" },
                        { title: qsTr("Calendula Gold / é‡‘ç›èŠ±"), desc: qsTr("æ´»åŠ›ã€ç§¯æ"), color: DelColorGenerator.Preset_Gold, colorName: "gold" },
                        { title: qsTr("Sunrise Yellow / æ—¥å‡º"), desc: qsTr("å‡ºç”Ÿã€é˜³å…‰"), color: DelColorGenerator.Preset_Yellow, colorName: "yellow" },
                        { title: qsTr("Lime / é’æŸ "), desc: qsTr("è‡ªç„¶ã€ç”Ÿæœº"), color: DelColorGenerator.Preset_Lime, colorName: "lime" },
                        { title: qsTr("Polar Green / æå…‰ç»¿"), desc: qsTr("å¥åº·ã€åˆ›æ–°"), color: DelColorGenerator.Preset_Green, colorName: "green" },
                        { title: qsTr("Cyan / æ˜é’"), desc: qsTr("å¸Œæœ›ã€åšå¼º"), color: DelColorGenerator.Preset_Cyan, colorName: "cyan" },
                        { title: qsTr("Daybreak Blue / æ‹‚æ™“è“"), desc: qsTr("åŒ…å®¹ã€ç§‘æŠ€ã€æ™®æƒ "), color: DelColorGenerator.Preset_Blue, colorName: "blue" },
                        { title: qsTr("Geek Blue / æå®¢è“"), desc: qsTr("æ¢ç´¢ã€é’»ç ”"), color: DelColorGenerator.Preset_Geekblue, colorName: "geekblue" },
                        { title: qsTr("Golden Purple / é…±ç´«"), desc: qsTr("ä¼˜é›…ã€æµªæ¼«"), color: DelColorGenerator.Preset_Purple, colorName: "purple" },
                        { title: qsTr("Magenta / æ³•å¼æ´‹çº¢"), desc: qsTr("æ˜å¿«ã€æ„Ÿæ€§"), color: DelColorGenerator.Preset_Magenta, colorName: "magenta" }
                    ]
                    delegate: ColorView {
                        id: rootItem
                        Layout.alignment: Qt.AlignCenter
                        title: modelData.title
                        desc: modelData.desc
                        colorName: modelData.colorName

                        Connections {
                            target: window
                            function onLightChanged() {
                                rootItem.model = colorGenerator.generate(modelData.color, window.light, window.light ? "#ffffff" : "#141414");
                            }
                        }
                    }
                }
            }
        }

        Item {
            id: discoverTab

            RowLayout{
                anchors.fill: parent

                Item {
                    Layout.preferredWidth: parent.width * 0.5
                    Layout.fillHeight: true

                    ColorView {
                        id: customView
                        width: 600
                        anchors.centerIn: parent
                        lineWidth: 600
                        lineHeight: 50
                        stretch: 30
                        colorName: "color"

                        Connections {
                            target: window
                            function onLightChanged() {
                                customView.model = colorGenerator.generate(colorSelect.currentColor, window.light, window.light ? "#f0f0f0" : "#141414");
                            }
                        }
                    }
                }

                Item {
                    Layout.preferredWidth: parent.width * 0.5
                    Layout.fillHeight: true

                    DelColorPicker {
                        id: colorSelect
                        anchors.centerIn: parent
                        initColor: "red"
                        movable: false
                        onCurrentColorChanged: {
                            customView.model = colorGenerator.generate(colorSelect.currentColor, window.light, window.light ? "#f0f0f0" : "#141414");
                        }
                        Component.onCompleted: open();
                    }
                }
            }
        }
    }
}
